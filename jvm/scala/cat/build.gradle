plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'com.github.spotbugs' version '1.6.9'
    id 'org.sonarqube' version '2.7'
}

apply plugin: 'scala'
apply plugin: 'checkstyle'
apply plugin: 'com.github.spotbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'

compileJava {
    options.compilerArgs << '-Xlint:all'
}

checkstyle {
    configFile = "${rootProject.projectDir}/config/checkstyle/checkstyle.xml" as File
    configProperties = [configDir: "${rootProject.projectDir}/config"]
}

spotbugs {
    sourceSets = [sourceSets.main]
    excludeFilter = file("${rootProject.projectDir}/config/spotbugs/exclude.xml")
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    consoleOutput = true
    ruleSets = [] // Replace; Don't append
    ruleSetFiles = files("config/pmd/rulesets.xml")
}

jar {
    manifest {
        attributes 'Main-Class': 'us.yellosoft.cat.Cat'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.scala-lang:scala-library:2.12.4'

    testCompile 'junit:junit:4.12'
    testCompile 'org.slf4j:slf4j-log4j12:1.7.12'
}
